plugins {
  id 'theodolite.java-conventions'
  id 'application' // executable
  id 'com.github.johnrengelman.shadow' // create fat jar
}

applicationDefaultJvmArgs = ["-Dlog4j.configuration=log4j.properties"]

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                   'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    zip64 true
}

tasks.distZip.enabled = false

ext {
  flinkVersion = '1.12.2'
  scalaBinaryVersion = '2.12'
}

repositories {
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url 'https://packages.confluent.io/maven/'
  }
}

dependencies {
    // Special version required because of https://issues.apache.org/jira/browse/FLINK-13703
    implementation('org.industrial-devops:titan-ccp-common:0.1.0-flink-ready-SNAPSHOT') { changing = true }
    implementation('org.industrial-devops:titan-ccp-common-kafka:0.1.0-SNAPSHOT') { changing = true }

    implementation 'org.apache.kafka:kafka-clients:2.2.0'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.slf4j:slf4j-simple:1.6.1'
    implementation project(':flink-commons')

    implementation "org.apache.flink:flink-java:${flinkVersion}"
    implementation "org.apache.flink:flink-streaming-java_${scalaBinaryVersion}:${flinkVersion}"
    implementation "org.apache.flink:flink-connector-kafka_${scalaBinaryVersion}:${flinkVersion}"
    implementation "org.apache.flink:flink-avro:${flinkVersion}"
    implementation "org.apache.flink:flink-avro-confluent-registry:${flinkVersion}"
    implementation "org.apache.flink:flink-statebackend-rocksdb_${scalaBinaryVersion}:${flinkVersion}"
    implementation "org.apache.flink:flink-metrics-prometheus_${scalaBinaryVersion}:${flinkVersion}"
    implementation "org.apache.flink:flink-runtime-web_${scalaBinaryVersion}:${flinkVersion}" // For debugging
    
    

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}
